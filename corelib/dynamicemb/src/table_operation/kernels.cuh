/******************************************************************************
# SPDX-FileCopyrightText: Copyright (c) 2025 NVIDIA CORPORATION & AFFILIATES.
All rights reserved. # SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
******************************************************************************/

#pragma once

#include "types.cuh"
#include <cub/cub.cuh>

#include <cooperative_groups.h>

using namespace cooperative_groups;
namespace cg = cooperative_groups;

namespace dyn_emb {

template <int ThreadBlockDim_, int ProbingGroupSize_, int ReductionGroupSize_,
          int CompactTileSize_, int NumScorePerThread_,
          ScorePolicyType PolicyType_ = ScorePolicyType::Const,
          bool OutputScore_ = false, bool EnableOverflow_ = false>
struct InsertKernelTraits {
  static constexpr int ThreadBlockDim = ThreadBlockDim_;
  static constexpr int ProbingGroupSize = ProbingGroupSize_;
  static constexpr int ReductionGroupSize = ReductionGroupSize_;
  static constexpr int CompactTileSize = CompactTileSize_;
  static constexpr int NumScorePerThread = NumScorePerThread_;
  static constexpr ScorePolicyType PolicyType = PolicyType_;
  static constexpr bool OutputScore = OutputScore_;
  static constexpr bool EnableOverflow = EnableOverflow_;
};

template <bool Pred> struct ExportPredFunctor {
  ScoreType threshold;
  ExportPredFunctor(ScoreType threshold) : threshold(threshold) {}

  __forceinline__ __device__ bool operator()(const ScoreType score) {
    if constexpr (Pred) {
      return score >= threshold;
    } else {
      return true;
    }
  }
};

// Increment the counter when matched
struct EvalAndCount {
  ScoreType threshold;
  CounterType *d_counter;

  EvalAndCount(ScoreType threshold, CounterType *d_counter)
      : threshold(threshold), d_counter(d_counter) {}

  template <int GroupSize>
  __forceinline__ __device__ void
  operator()(const ScoreType score, cg::thread_block_tile<GroupSize> &g,
             bool valid) {

    bool match = valid && (score >= threshold);

    uint32_t vote = g.ballot(match);
    int group_cnt = __popc(vote);
    if (g.thread_rank() == 0) {
      atomicAdd(d_counter, static_cast<CounterType>(group_cnt));
    }
  }
};

template <typename Table, int ProbingGroupSize, ScorePolicyType PolicyType,
          bool EnableOverflow = false>
__global__ void table_lookup_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int64_t batch, typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids, bool *__restrict__ founds,
    IndexType *__restrict__ indices, ScoreType *__restrict__ score_input,
    int64_t *__restrict__ score_output, Table ovf_table,
    int64_t const *__restrict__ ovf_output_offsets) {

  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {

    KeyType key = input_keys[i];
    ScoreType score = ScorePolicy<PolicyType>::get(score_input, i);

    int64_t hashcode = 0;
    int64_t bucket_id = 0;
    int64_t bkt_begin = 0;
    int64_t table_cap = 0;
    int64_t t_id = 0;
    if (Bucket::is_valid(key)) {
      hashcode = Table::hash(key);
      t_id = table_ids[i];
      bkt_begin = table_bucket_offsets[t_id];
      int64_t bkt_end = table_bucket_offsets[t_id + 1];
      table_cap = (bkt_end - bkt_begin) * table.bucket_capacity();
      if (table_cap > 0) {
        int64_t local_idx = hashcode % table_cap;
        bucket_id = bkt_begin + local_idx / table.bucket_capacity();
      }
    }
    if (table_cap == 0) {
      score_output[i] = static_cast<int64_t>(score);
      founds[i] = false;
      indices[i] = -1;
      continue;
    }
    Bucket bucket = table[bucket_id];
    Iter iter = Iter(hashcode % table.bucket_capacity());
    int64_t step = 0;
    auto probe_res = bucket.probe<ProbingGroupSize>(key, iter, step);
    bool found = probe_res == Bucket::ProbeResult::Existed;
    IndexType index = -1;
    if (found) {
      if constexpr (PolicyType == ScorePolicyType::Const) {
        // Return the reduction score (frequency for LruLfu; word 0 otherwise),
        // not word 0 which for LruLfu is the timestamp.
        score = *bucket.reduction_score(iter);
      } else {
        KeyType expected_key = key;
        if (bucket.try_lock(iter, expected_key)) {
          // For LruLfu, update() writes both words (timestamp + frequency) and
          // returns the frequency.
          score = ScorePolicy<PolicyType>::update(bucket.scores(iter), score);
          bucket.unlock(iter, key);
        } else {
          found = false;
          score = ScoreType();
        }
      }

      if (found) {
        index = (bucket_id - bkt_begin) * bucket.capacity() + iter;
      }
    }

    if constexpr (EnableOverflow) {
      if (!found && Bucket::is_valid(key)) {
        Bucket ovf_bucket = ovf_table[t_id];
        Iter ovf_iter = Iter(hashcode % ovf_bucket.capacity());
        Iter ovf_out_iter;
        bool ovf_found = overflow_find(
            ovf_bucket, key, ovf_output_offsets[t_id], ovf_iter, &ovf_out_iter);
        if (ovf_found) {
          found = true;
          index = ovf_out_iter;
          Iter local = ovf_out_iter - ovf_output_offsets[t_id];
          if constexpr (PolicyType == ScorePolicyType::Const) {
            // Reduction score (frequency for LruLfu), not word 0 (timestamp).
            score = *ovf_bucket.reduction_score(local);
          } else {
            // Maintain the overflow entry's score on access (stamp timestamp /
            // accumulate frequency), mirroring the main-table found path; for
            // LruLfu update() writes both words. Lock the slot for a consistent
            // write; skip if the entry moved out concurrently.
            KeyType expected_key = key;
            if (ovf_bucket.try_lock(local, expected_key)) {
              score = ScorePolicy<PolicyType>::update(ovf_bucket.scores(local),
                                                      score);
              ovf_bucket.unlock(local, key);
            }
          }
        }
      }
    }

    score_output[i] = static_cast<int64_t>(score);
    founds[i] = found;
    indices[i] = index;
  }
}

template <int ProbingGroupSize, typename Bucket, typename KeyType>
__forceinline__ __device__ void
insert_probe(Bucket &bucket, KeyType key, int *__restrict__ bucket_sizes,
             int64_t bucket_id, typename Bucket::Iterator iter_in,
             typename Bucket::Iterator *iter_out, InsertResult *result_out) {
  auto iter = iter_in;
  auto result = InsertResult::Init;
  using ProbeResult = typename Bucket::ProbeResult;
  ProbeResult probe_res = ProbeResult::Init;
  int64_t step = 0;
  while (step != bucket.capacity()) {
    probe_res = bucket.template probe<ProbingGroupSize>(key, iter, step);
    if (probe_res == ProbeResult::Existed) {
      KeyType expected_key = key;
      if (bucket.try_lock(iter, expected_key)) {
        result = InsertResult::Assign;
      }
      break;
    }
    if (probe_res == ProbeResult::Empty) {
      KeyType expected_key = Bucket::empty_key();
      if (bucket.try_lock(iter, expected_key)) {
        *bucket.digests(iter) = Bucket::key_to_digest(key);
        atomicAdd(&bucket_sizes[bucket_id], 1);
        result = InsertResult::Insert;
        break;
      }
    }
    if (probe_res == ProbeResult::Failed) {
      result = InsertResult::Illegal;
      break;
    }
  }
  *iter_out = iter;
  *result_out = result;
}

// Eviction victim selection, pluggable so the same insert()/kernel body serves
// both the AoT build and the LruLfu evict cubin. DefaultEvictor is the existing
// score-word reduce() (used by every AoT instantiation; cur_ts/gamma ignored).
// RankedEvictor<Comparator> drives reduce_ranked<Comparator>() (the LruLfu cubin;
// e.g. LexFreqTsComparator or UserFnComparator).
struct DefaultEvictor {
  template <int ReductionGroupSize, int BufferDim, typename Bucket>
  __forceinline__ __device__ bool
  operator()(Bucket &bucket, typename Bucket::Iterator &iter,
             typename Bucket::KeyType &evict_key, ScoreType &evict_score,
             ScoreType *sm_scores, int32_t const *__restrict__ counter,
             int64_t counter_offset, uint64_t /*cur_ts*/) const {
    return bucket.template reduce<ReductionGroupSize, BufferDim>(
        iter, evict_key, evict_score, sm_scores, counter, counter_offset);
  }
};

template <typename Comparator> struct RankedEvictor {
  template <int ReductionGroupSize, int BufferDim, typename Bucket>
  __forceinline__ __device__ bool
  operator()(Bucket &bucket, typename Bucket::Iterator &iter,
             typename Bucket::KeyType &evict_key, ScoreType &evict_score,
             ScoreType *sm_scores, int32_t const *__restrict__ counter,
             int64_t counter_offset, uint64_t cur_ts) const {
    return bucket.template reduce_ranked<Comparator, ReductionGroupSize,
                                         BufferDim>(
        iter, evict_key, evict_score, sm_scores, counter, counter_offset,
        cur_ts);
  }
};

template <int ReductionGroupSize, int BufferDim, typename Policy,
          typename Evictor = DefaultEvictor, typename Bucket, typename KeyType>
__forceinline__ __device__ void
insert(Bucket &bucket, KeyType key, ScoreType score,
       int *__restrict__ bucket_sizes, int64_t bucket_id,
       typename Bucket::Iterator iter_in, ScoreType *sm_scores,
       InsertResult result_in, typename Bucket::Iterator *iter_out,
       InsertResult *result_out, KeyType *evict_key_out,
       ScoreType *evict_score_out, int32_t *__restrict__ counter,
       int64_t counter_offset, uint64_t cur_ts = 0) {
  auto iter = iter_in;
  auto result = result_in;
  Evictor evictor;
  while (result == InsertResult::Init) {
    KeyType evict_key;
    ScoreType evict_score = Policy::score_for_compare(score);

    bool succeed = evictor.template operator()<ReductionGroupSize, BufferDim>(
        bucket, iter, evict_key, evict_score, sm_scores, counter,
        counter_offset, cur_ts);

    if (succeed) {
      if (bucket.try_lock(iter, evict_key)) {
        if (evict_key != Bucket::reclaimed_key()) {
          int64_t flat_idx = counter_offset + iter;
          if (::atomicAdd(&counter[flat_idx], 0) > 0) {
            bucket.unlock(iter, evict_key);
            continue;
          }
        }
        if (*bucket.reduction_score(iter) != evict_score) {
          bucket.unlock(iter, evict_key);
        } else {
          *bucket.digests(iter) = Bucket::key_to_digest(key);
          if (evict_key == Bucket::reclaimed_key()) {
            atomicAdd(&bucket_sizes[bucket_id], 1);
	    ScoreType *sblk = bucket.scores(iter);
	    for (int64_t s = 0; s < bucket.num_scores(); ++s)
              sblk[s] = ScoreType();
            result = InsertResult::Reclaim;
          } else {
            // Clear the evicted key's whole (AoS-contiguous) score block so the
            // new occupant starts fresh (e.g. LruLfu frequency restarts at 0).
            ScoreType *sblk = bucket.scores(iter);
            for (int64_t s = 0; s < bucket.num_scores(); ++s)
              sblk[s] = ScoreType();
            result = InsertResult::Evict;
          }
          if (evict_key_out)
            *evict_key_out = evict_key;
          if (evict_score_out)
            *evict_score_out = evict_score;
          break;
        }
      }
    } else {
      result = InsertResult::Busy;
      if (evict_key_out)
        *evict_key_out = key;
      if (evict_score_out)
        *evict_score_out = score;
      break;
    }
  }
  *iter_out = iter;
  *result_out = result;
}

// Shared plain-insert body, pluggable on Evictor (mirrors insert_and_evict_body).
// DefaultEvictor (AoT) uses the single-score reduce(); the LruLfu insert cubin
// (evict_lrulfu.cu) instantiates it with RankedEvictor<Comparator> so a full
// bucket on a 2-word table evicts by the same comparator as insert_and_evict,
// never the single-score reduce(). Plain insert drops the evicted key (no
// evicted-output collection), so cur_ts is the only extra input the ranked
// evictor needs.
// CollectEvicted=true additionally writes each evicted victim's (key, table_id)
// to evicted_keys / evicted_table_ids (compacted; count in evicted_counter) --
// the "retain evicted keys" hook for a last-tier table. Only InsertResult::Evict
// (an existing key pushed out) is collected, never Busy. Default false keeps the
// plain-insert path (drops the victim) byte-for-byte unchanged (if constexpr).
template <typename Table, typename KernelTraits,
          typename Evictor = DefaultEvictor, bool CollectEvicted = false>
__device__ __forceinline__ void
insert_body(Table table, int64_t const *__restrict__ table_bucket_offsets,
            int *__restrict__ bucket_sizes, int64_t batch,
            typename Table::KeyType const *__restrict__ input_keys,
            int64_t const *__restrict__ table_ids,
            InsertResult *__restrict__ insert_results,
            IndexType *__restrict__ indices, ScoreType *__restrict__ score_input,
            int64_t *__restrict__ score_output,
            typename Table::KeyType **__restrict__ table_key_slots,
            int32_t *__restrict__ counter, uint64_t cur_ts = 0,
            typename Table::KeyType *__restrict__ evicted_keys = nullptr,
            int64_t *__restrict__ evicted_table_ids = nullptr,
            CounterType *evicted_counter = nullptr) {

  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  static constexpr int BlockSize = KernelTraits::ThreadBlockDim;
  static constexpr int BufferDim = KernelTraits::NumScorePerThread;

  static constexpr int ProbingGroupSize = KernelTraits::ProbingGroupSize;
  static constexpr int ReductionGroupSize = KernelTraits::ReductionGroupSize;
  static constexpr ScorePolicyType PolicyType = KernelTraits::PolicyType;
  static constexpr bool OutputScore = KernelTraits::OutputScore;

  using Policy = ScorePolicy<PolicyType>;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  __shared__ ScoreType sm_scores[BlockSize * BufferDim];

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {

    KeyType key = input_keys[i];
    ScoreType score = Policy::get(score_input, i);

    int64_t hashcode = 0;
    int64_t bucket_id = 0;
    int64_t bkt_begin = 0;
    int64_t table_cap = 0;
    if (Bucket::is_valid(key)) {
      hashcode = Table::hash(key);
      int64_t t_id = table_ids[i];
      bkt_begin = table_bucket_offsets[t_id];
      int64_t bkt_end = table_bucket_offsets[t_id + 1];
      table_cap = (bkt_end - bkt_begin) * table.bucket_capacity();
      if (table_cap > 0) {
        int64_t local_idx = hashcode % table_cap;
        bucket_id = bkt_begin + local_idx / table.bucket_capacity();
      }
    }
    if (table_cap == 0) {
      if constexpr (OutputScore) {
        score_output[i] = static_cast<int64_t>(score);
      }
      table_key_slots[i] = nullptr;
      indices[i] = -1;
      if (insert_results) {
        insert_results[i] = InsertResult::Illegal;
      }
      continue;
    }

    InsertResult result;
    Bucket bucket = table[bucket_id];
    Iter iter = Iter(hashcode % table.bucket_capacity());
    insert_probe<ProbingGroupSize>(bucket, key, bucket_sizes, bucket_id, iter,
                                   &iter, &result);
    int64_t counter_offset = (bucket_id - bkt_begin) * bucket.capacity();
    KeyType evict_key = KeyType();
    insert<ReductionGroupSize, BufferDim, Policy, Evictor>(
        bucket, key, score, bucket_sizes, bucket_id, iter, sm_scores, result,
        &iter, &result,
        CollectEvicted ? &evict_key : static_cast<KeyType *>(nullptr),
        static_cast<ScoreType *>(nullptr), counter, counter_offset, cur_ts);

    IndexType index = -1;
    KeyType *table_key_slot = nullptr;
    if (isInsertSuccess(result)) {
      // For LruLfu, update() writes both words (timestamp + frequency); a freshly
      // evicted slot already had its block cleared in insert().
      score = Policy::update(bucket.scores(iter), score);
      index = (bucket_id - bkt_begin) * bucket.capacity() + iter;
      table_key_slot = bucket.keys(iter);
    }
    if constexpr (OutputScore) {
      // Null-safe so the LruLfu insert cubin can fix OutputScore=true and decide
      // at runtime (score_output may be null). For AoT, OutputScore=true always
      // pairs with a non-null score_output, so the check is free there.
      if (score_output != nullptr)
        score_output[i] = static_cast<int64_t>(score);
    }
    table_key_slots[i] = table_key_slot;
    indices[i] = index;
    if (insert_results) {
      insert_results[i] = result;
    }

    if constexpr (CollectEvicted) {
      // Retain-evicted-keys hook: record the (victim key, table_id) of an
      // eviction. CompactTileSize == 1 for every insert instantiation
      // (InsertKernelTraits<...,/*CompactTileSize=*/1,...> in insert.cu and
      // evict_lrulfu.cu), so this size-1 tile ballot needs no cross-thread
      // convergence and the `continue` on table_cap==0 above is harmless (those
      // threads evicted nothing). Only Evict is collected, not Busy.
      auto g = cg::tiled_partition<KernelTraits::CompactTileSize>(
          cg::this_thread_block());
      bool evicted = (result == InsertResult::Evict);
      uint32_t vote = g.ballot(evicted);
      int group_cnt = __popc(vote);
      CounterType group_offset = 0;
      if (g.thread_rank() == 0)
        group_offset =
            atomicAdd(evicted_counter, static_cast<CounterType>(group_cnt));
      group_offset = g.shfl(group_offset, 0);
      int previous_cnt = group_cnt - __popc(vote >> g.thread_rank());
      int64_t out_id = group_offset + previous_cnt;
      if (evicted) {
        evicted_keys[out_id] = evict_key;
        evicted_table_ids[out_id] = table_ids[i];
      }
    }
  }
}

// AoT entry: plain insert with the single-score reduce (DefaultEvictor). The
// LruLfu insert cubin provides its own __global__ that calls insert_body with
// RankedEvictor<Comparator> (see evict_lrulfu.cu).
template <typename Table, typename KernelTraits>
__global__ void table_insert_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids,
    InsertResult *__restrict__ insert_results, IndexType *__restrict__ indices,
    ScoreType *__restrict__ score_input, int64_t *__restrict__ score_output,
    typename Table::KeyType **__restrict__ table_key_slots,
    int32_t *__restrict__ counter) {
  insert_body<Table, KernelTraits, DefaultEvictor>(
      table, table_bucket_offsets, bucket_sizes, batch, input_keys, table_ids,
      insert_results, indices, score_input, score_output, table_key_slots,
      counter, /*cur_ts=*/0);
}

// AoT entry: plain insert that also retains each evicted victim's (key,
// table_id). Mirrors table_insert_kernel with CollectEvicted=true; used by the
// last-tier retain path for non-LruLfu score policies. The LruLfu insert cubin
// has its own dyn_emb_insert_collect_entry (see evict_lrulfu.cu).
template <typename Table, typename KernelTraits>
__global__ void table_insert_collect_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids,
    InsertResult *__restrict__ insert_results, IndexType *__restrict__ indices,
    ScoreType *__restrict__ score_input, int64_t *__restrict__ score_output,
    typename Table::KeyType **__restrict__ table_key_slots,
    int32_t *__restrict__ counter, CounterType *evicted_counter,
    typename Table::KeyType *__restrict__ evicted_keys,
    int64_t *__restrict__ evicted_table_ids) {
  insert_body<Table, KernelTraits, DefaultEvictor, /*CollectEvicted=*/true>(
      table, table_bucket_offsets, bucket_sizes, batch, input_keys, table_ids,
      insert_results, indices, score_input, score_output, table_key_slots,
      counter, /*cur_ts=*/0, evicted_keys, evicted_table_ids, evicted_counter);
}

template <typename Table, typename KernelTraits, typename Evictor>
__device__ __forceinline__ void insert_and_evict_body(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids,
    InsertResult *__restrict__ insert_results, IndexType *__restrict__ indices,
    ScoreType *__restrict__ score_input, int64_t *__restrict__ score_output,
    typename Table::KeyType **__restrict__ table_key_slots,
    CounterType *evicted_counter,
    typename Table::KeyType *__restrict__ evicted_keys,
    int64_t *__restrict__ evicted_scores,
    IndexType *__restrict__ evicted_indices,
    int64_t *__restrict__ evicted_table_ids, int32_t *__restrict__ counter,
    Table ovf_table, int *__restrict__ ovf_bucket_sizes,
    int32_t *__restrict__ ovf_counter,
    int64_t const *__restrict__ ovf_output_offsets, uint64_t cur_ts) {

  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  static constexpr int BlockSize = KernelTraits::ThreadBlockDim;
  static constexpr int BufferDim = KernelTraits::NumScorePerThread;

  static constexpr int ProbingGroupSize = KernelTraits::ProbingGroupSize;
  static constexpr int ReductionGroupSize = KernelTraits::ReductionGroupSize;
  static constexpr ScorePolicyType PolicyType = KernelTraits::PolicyType;
  static constexpr bool OutputScore = KernelTraits::OutputScore;
  static constexpr bool UseOverflow = KernelTraits::EnableOverflow;

  using Policy = ScorePolicy<PolicyType>;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  __shared__ ScoreType sm_scores[BlockSize * BufferDim];

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {

    KeyType key = input_keys[i];
    ScoreType score = Policy::get(score_input, i);

    int64_t hashcode = 0;
    int64_t bucket_id = 0;
    int64_t bkt_begin = 0;
    int64_t table_cap = 0;
    int64_t t_id = 0;
    if (Bucket::is_valid(key)) {
      hashcode = Table::hash(key);
      t_id = table_ids[i];
      bkt_begin = table_bucket_offsets[t_id];
      int64_t bkt_end = table_bucket_offsets[t_id + 1];
      table_cap = (bkt_end - bkt_begin) * table.bucket_capacity();
      if (table_cap > 0) {
        int64_t local_idx = hashcode % table_cap;
        bucket_id = bkt_begin + local_idx / table.bucket_capacity();
      }
    }
    InsertResult result = InsertResult::Illegal;
    Bucket bucket = table[bucket_id];
    Iter iter = Iter();
    KeyType evict_key = KeyType();
    ScoreType evict_score = ScoreType();

    if (table_cap > 0) {
      iter = Iter(hashcode % table.bucket_capacity());
      insert_probe<ProbingGroupSize>(bucket, key, bucket_sizes, bucket_id, iter,
                                     &iter, &result);

      {
        int64_t counter_offset = (bucket_id - bkt_begin) * bucket.capacity();
        insert<ReductionGroupSize, BufferDim, Policy, Evictor>(
            bucket, key, score, bucket_sizes, bucket_id, iter, sm_scores,
            result, &iter, &result, &evict_key, &evict_score, counter,
            counter_offset, cur_ts);
      }
    }

    IndexType index = -1;
    KeyType *table_key_slot = nullptr;
    if (isInsertSuccess(result)) {
      // For LruLfu, update() writes both words (timestamp + frequency).
      score = Policy::update(bucket.scores(iter), score);
      index = (bucket_id - bkt_begin) * bucket.capacity() + iter;
      table_key_slot = bucket.keys(iter);
    }

    // Overflow fallback for Busy results
    KeyType final_evict_key = evict_key;
    ScoreType final_evict_score = evict_score;
    IndexType final_evict_index = -static_cast<IndexType>(i + 1);
    InsertResult final_result = result;

    if constexpr (UseOverflow) {
      if (result == InsertResult::Busy && Bucket::is_valid(key)) {
        Bucket ovf_bucket = ovf_table[t_id];
        int64_t ovf_bucket_capacity = ovf_bucket.capacity();
        Iter ovf_iter = Iter(hashcode % ovf_bucket_capacity);
        InsertResult ovf_result = InsertResult::Init;
        KeyType ovf_evict_key = KeyType();
        int32_t *ovf_counter_table = ovf_counter + t_id * ovf_bucket_capacity;
        overflow_insert_and_evict(ovf_bucket, key, ovf_bucket_sizes, t_id,
                                  ovf_counter_table, ovf_output_offsets[t_id],
                                  ovf_iter, &ovf_iter, &ovf_result,
                                  &ovf_evict_key);

        if (isInsertSuccess(ovf_result)) {
          index = ovf_iter;
          final_result = ovf_result;
          table_key_slot = nullptr;
          Iter local = ovf_iter - ovf_output_offsets[t_id];
          if (ovf_result == InsertResult::Insert ||
              ovf_result == InsertResult::Evict) {
            // Newly occupied overflow slot (locked until table_unlock_kernel):
            // clear the whole score block so LruLfu frequency restarts at 0,
            // then write the score words via the policy.
            for (int64_t s = 0; s < ovf_bucket.num_scores(); ++s)
              *ovf_bucket.scores(local, s) = ScoreType();
            score = Policy::update(ovf_bucket.scores(local), score);
            table_key_slot = ovf_bucket.keys(local);
            if (ovf_result == InsertResult::Evict) {
              final_evict_key = ovf_evict_key;
              final_evict_index = ovf_iter;
            }
          } else if (ovf_result == InsertResult::Assign) {
            // Existing overflow key re-inserted: update its score under lock
            // (accumulate for LruLfu). It is not locked by the find, so guard.
            KeyType expected_key = key;
            if (ovf_bucket.try_lock(local, expected_key)) {
              score = Policy::update(ovf_bucket.scores(local), score);
              ovf_bucket.unlock(local, key);
            }
          }
        }
      }

      if (result == InsertResult::Evict) {
        final_evict_index = (bucket_id - bkt_begin) * bucket.capacity() + iter;
      }
    }

    // Eviction compaction -- all threads participate in ballot
    {
      auto g = cg::tiled_partition<KernelTraits::CompactTileSize>(
          cg::this_thread_block());

      InsertResult cmp_result = UseOverflow ? final_result : result;
      bool evicted = (cmp_result == InsertResult::Evict ||
                      cmp_result == InsertResult::Busy);
      uint32_t vote = g.ballot(evicted);
      int group_cnt = __popc(vote);
      CounterType group_offset = 0;
      if (g.thread_rank() == 0) {
        group_offset =
            atomicAdd(evicted_counter, static_cast<CounterType>(group_cnt));
      }
      group_offset = g.shfl(group_offset, 0);
      int previous_cnt = group_cnt - __popc(vote >> g.thread_rank());
      int64_t out_id = group_offset + previous_cnt;
      if (evicted) {
        if constexpr (UseOverflow) {
          evicted_keys[out_id] = final_evict_key;
          evicted_scores[out_id] = static_cast<int64_t>(final_evict_score);
          evicted_indices[out_id] = final_evict_index;
        } else {
          evicted_keys[out_id] = evict_key;
          evicted_scores[out_id] = static_cast<int64_t>(evict_score);
          IndexType evict_idx =
              (result == InsertResult::Evict)
                  ? (bucket_id - bkt_begin) * bucket.capacity() + iter
                  : -static_cast<IndexType>(i + 1);
          evicted_indices[out_id] = evict_idx;
        }
        evicted_table_ids[out_id] = table_ids[i];
      }
    }

    if constexpr (OutputScore) {
      // Null-safe so the LruLfu evict cubin can fix OutputScore=true and decide
      // at runtime (score_output may be null). For AoT, OutputScore=true always
      // pairs with a non-null score_output, so the check is free there.
      if (score_output != nullptr)
        score_output[i] = static_cast<int64_t>(score);
    }
    table_key_slots[i] = table_key_slot;
    indices[i] = index;
    if (insert_results) {
      insert_results[i] = UseOverflow ? final_result : result;
    }
  }
}

// AoT entry: the existing insert-and-evict kernel, using the score-word reduce
// (DefaultEvictor). Unchanged ABI for all existing instantiations. The LruLfu
// evict cubin provides its own __global__ that calls insert_and_evict_body with
// RankedEvictor<Comparator> (see evict_lrulfu.cu).
template <typename Table, typename KernelTraits>
__global__ void table_insert_and_evict_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids,
    InsertResult *__restrict__ insert_results, IndexType *__restrict__ indices,
    ScoreType *__restrict__ score_input, int64_t *__restrict__ score_output,
    typename Table::KeyType **__restrict__ table_key_slots,
    CounterType *evicted_counter,
    typename Table::KeyType *__restrict__ evicted_keys,
    int64_t *__restrict__ evicted_scores,
    IndexType *__restrict__ evicted_indices,
    int64_t *__restrict__ evicted_table_ids, int32_t *__restrict__ counter,
    Table ovf_table, int *__restrict__ ovf_bucket_sizes,
    int32_t *__restrict__ ovf_counter,
    int64_t const *__restrict__ ovf_output_offsets) {
  insert_and_evict_body<Table, KernelTraits, DefaultEvictor>(
      table, table_bucket_offsets, bucket_sizes, batch, input_keys, table_ids,
      insert_results, indices, score_input, score_output, table_key_slots,
      evicted_counter, evicted_keys, evicted_scores, evicted_indices,
      evicted_table_ids, counter, ovf_table, ovf_bucket_sizes, ovf_counter,
      ovf_output_offsets, /*cur_ts=*/0);
}

template <typename Table>
__global__ void
table_unlock_kernel(Table table, int64_t batch,
                    typename Table::KeyType const *__restrict__ input_keys,
                    typename Table::KeyType **__restrict__ table_key_slots) {
  using KeyType = typename Table::KeyType;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {
    KeyType key = input_keys[i];
    KeyType *key_slot = table_key_slots[i];
    if (key_slot) {
      *key_slot = key;
    }
  }
}

template <typename Table, int ProbingGroupSize>
__global__ void table_erase_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids, IndexType *__restrict__ indices) {

  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {

    KeyType key = input_keys[i];

    int64_t hashcode = 0;
    int64_t bucket_id = 0;
    int64_t bkt_begin = 0;
    int64_t table_cap = 0;
    if (Bucket::is_valid(key)) {
      hashcode = Table::hash(key);
      int64_t t_id = table_ids[i];
      bkt_begin = table_bucket_offsets[t_id];
      int64_t bkt_end = table_bucket_offsets[t_id + 1];
      table_cap = (bkt_end - bkt_begin) * table.bucket_capacity();
      if (table_cap > 0) {
        int64_t local_idx = hashcode % table_cap;
        bucket_id = bkt_begin + local_idx / table.bucket_capacity();
      }
    }
    if (table_cap == 0) {
      if (indices) {
        indices[i] = -1;
      }
      continue;
    }

    Bucket bucket = table[bucket_id];
    Iter iter = Iter(hashcode % table.bucket_capacity());
    int64_t step = 0;
    auto probe_res = bucket.probe<ProbingGroupSize>(key, iter, step);
    bool found = probe_res == Bucket::ProbeResult::Existed;
    IndexType index = -1;
    if (found) {
      KeyType expected_key = key;
      if (bucket.try_lock(iter, expected_key)) {
        *bucket.scores(iter) = ScoreType();
        *bucket.digests(iter) = Bucket::empty_digest();

        bucket.unlock(iter, Bucket::reclaimed_key());
        atomicSub(bucket_sizes + bucket_id, 1);
      } else {
        found = false; // only one update will succeed for duplicated keys.
      }

      if (found) {
        index = (bucket_id - bkt_begin) * bucket.capacity() + iter;
      }
    }
    if (indices) {
      indices[i] = index;
    }
  }
}

template <typename Table, typename PredFunctor, int TileSize>
__global__ void table_export_batch_kernel(
    Table table, IndexType begin, IndexType end, IndexType table_begin,
    CounterType *__restrict__ counter,
    typename Table::KeyType *__restrict__ keys, ScoreType *__restrict__ scores,
    PredFunctor pred, IndexType *__restrict__ indices,
    int64_t score_index = 0) {
  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  auto g = cg::tiled_partition<TileSize>(cg::this_thread_block());

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = begin + tid; i < end; i += gridDim.x * blockDim.x) {

    int64_t bucket_id = i / table.bucket_capacity();

    Bucket bucket = table[bucket_id];

    Iter iter = Iter(i % bucket.capacity());

    const KeyType key = *bucket.keys(iter);
    // Predicate + exported score read from the selected column (score_index).
    // col0 (default) is the reduction score; an auxiliary column (e.g. a
    // last-access timestamp) is used for time-based incremental dump.
    const ScoreType score = *bucket.scores(iter, score_index);
    const IndexType index = i - table_begin;

    bool valid = Bucket::is_valid(key);
    bool match = valid and pred.template operator()(score);
    // bool match = valid and pred(score);
    uint32_t vote = g.ballot(match);
    int group_cnt = __popc(vote);
    CounterType group_offset = 0;
    if (g.thread_rank() == 0) {
      group_offset = atomicAdd(counter, static_cast<CounterType>(group_cnt));
    }
    group_offset = g.shfl(group_offset, 0);

    int previous_cnt = group_cnt - __popc(vote >> g.thread_rank());
    int64_t out_id = group_offset + previous_cnt;

    if (match) {
      keys[out_id] = key;
      if (scores) {
        scores[out_id] = score;
      }
      if (indices) {
        indices[out_id] = index;
      }
    }
  }
}

// ---------------------------------------------------------------------------
// Overflow buffer: find a key by linear scan.
// Returns true if found, sets *iter_out = local_pos + output_offset.
// ---------------------------------------------------------------------------
template <typename Bucket, typename KeyType>
__forceinline__ __device__ bool
overflow_find(Bucket &ovf_bucket, KeyType key, int64_t output_offset,
              typename Bucket::Iterator start_iter,
              typename Bucket::Iterator *iter_out) {
  for (int64_t scan = 0; scan < ovf_bucket.capacity(); scan++) {
    auto pos = (start_iter + scan) % ovf_bucket.capacity();

    auto key_slot =
        reinterpret_cast<typename Bucket::AtomicKey *>(ovf_bucket.keys(pos));
    KeyType k = key_slot->load(cuda::std::memory_order_relaxed);

    if (k == key) {
      *iter_out = pos + output_offset;
      return true;
    }
    if (k == Bucket::empty_key()) {
      return false;
    }
  }
  return false;
}

// ---------------------------------------------------------------------------
// Overflow buffer: single-pass find-or-insert with counter-based eviction.
// Linear scan: key found -> Assign; empty -> Insert; counter==0 -> Evict.
// Outputs unified index (local + output_offset).
// ---------------------------------------------------------------------------
template <typename Bucket, typename KeyType>
__forceinline__ __device__ void overflow_insert_and_evict(
    Bucket &bucket, KeyType key, int *__restrict__ bucket_sizes,
    int64_t bucket_id, int32_t *__restrict__ counter, int64_t output_offset,
    typename Bucket::Iterator start_iter, typename Bucket::Iterator *iter_out,
    InsertResult *result_out, KeyType *evict_key_out) {

  for (int64_t scan = 0; scan < bucket.capacity(); scan++) {
    auto pos = (start_iter + scan) % bucket.capacity();

    auto key_slot =
        reinterpret_cast<typename Bucket::AtomicKey *>(bucket.keys(pos));
    KeyType k = key_slot->load(cuda::std::memory_order_relaxed);

    if (k == key) {
      *iter_out = pos + output_offset;
      *result_out = InsertResult::Assign;
      return;
    }

    if (k == Bucket::empty_key()) {
      KeyType expected = Bucket::empty_key();
      if (bucket.try_lock(pos, expected)) {
        *bucket.digests(pos) = Bucket::key_to_digest(key);
        atomicAdd(&bucket_sizes[bucket_id], 1);
        *iter_out = pos + output_offset;
        *result_out = InsertResult::Insert;
        return;
      }
      k = key_slot->load(cuda::std::memory_order_relaxed);
      if (k == key) {
        *iter_out = pos + output_offset;
        *result_out = InsertResult::Assign;
        return;
      }
      continue;
    }

    if (k == Bucket::LockedKey || k == Bucket::reclaimed_key())
      continue;

    if (counter[pos] == 0) {
      if (bucket.try_lock(pos, k)) {
        if (::atomicAdd(&counter[pos], 0) > 0) {
          bucket.unlock(pos, k);
          continue;
        }
        *bucket.digests(pos) = Bucket::key_to_digest(key);
        *evict_key_out = k;
        *iter_out = pos + output_offset;
        *result_out = InsertResult::Evict;
        return;
      }
      continue;
    }
  }

  *result_out = InsertResult::Busy;
  *evict_key_out = key;
}

template <typename Table, typename ExecFunctor, int TileSize>
__global__ void table_traverse_kernel(Table table, IndexType begin,
                                      IndexType end, ExecFunctor f,
                                      int64_t score_index = 0) {
  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  cg::thread_block_tile<TileSize> g =
      cg::tiled_partition<TileSize>(cg::this_thread_block());

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = begin + tid; i < end; i += gridDim.x * blockDim.x) {

    int64_t bucket_id = i / table.bucket_capacity();

    Bucket bucket = table[bucket_id];

    Iter iter = Iter(i % bucket.capacity());

    const KeyType key = *bucket.keys(iter);
    const ScoreType score = *bucket.scores(iter, score_index);

    bool valid = Bucket::is_valid(key);
    f.template operator()<TileSize>(score, g, valid);
  }
}

// Copy every score word for a set of (src_slot, dst_slot) key pairs from one
// table to another. Used by rehash to preserve multi-word score layouts (e.g.
// LruLfu's timestamp+frequency) that a single-value re-insert cannot restore.
// Slots are table-relative flat indices ((local_bucket * bucket_capacity) +
// iter); *_bkt_begin is the table's first global bucket. Reuses the AoS
// Bucket::scores(iter, k) accessor so no layout math is duplicated.
template <typename Table>
__global__ void copy_score_blocks_kernel(
    Table src_table, int64_t src_bkt_begin, Table dst_table,
    int64_t dst_bkt_begin, int64_t n, const int64_t *__restrict__ src_slots,
    const int64_t *__restrict__ dst_slots) {
  using Bucket = typename Table::BucketType;
  int64_t i = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
  if (i >= n)
    return;
  int64_t ss = src_slots[i];
  int64_t ds = dst_slots[i];
  if (ss < 0 || ds < 0)
    return;
  int64_t s_bc = src_table.bucket_capacity();
  int64_t d_bc = dst_table.bucket_capacity();
  Bucket sb = src_table[src_bkt_begin + ss / s_bc];
  Bucket db = dst_table[dst_bkt_begin + ds / d_bc];
  int64_t s_iter = ss % s_bc;
  int64_t d_iter = ds % d_bc;
  int64_t ns = dst_table.num_scores();
  for (int64_t k = 0; k < ns; ++k) {
    *db.scores(d_iter, k) = *sb.scores(s_iter, k);
  }
}

// Gather all score words for a set of slots into a row-major [n, num_scores]
// output. Used by dump to persist multi-word score layouts (LruLfu). Slots are
// table-relative flat indices; bkt_begin is the table's first global bucket.
template <typename Table>
__global__ void gather_score_blocks_kernel(
    Table table, int64_t bkt_begin, int64_t n,
    const int64_t *__restrict__ slots, ScoreType *__restrict__ out) {
  using Bucket = typename Table::BucketType;
  int64_t i = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
  if (i >= n)
    return;
  int64_t ns = table.num_scores();
  int64_t s = slots[i];
  if (s < 0) {
    for (int64_t k = 0; k < ns; ++k)
      out[i * ns + k] = ScoreType();
    return;
  }
  int64_t bc = table.bucket_capacity();
  Bucket b = table[bkt_begin + s / bc];
  int64_t it = s % bc;
  for (int64_t k = 0; k < ns; ++k)
    out[i * ns + k] = *b.scores(it, k);
}

// Scatter a row-major [n, num_scores] score block into a set of slots. Used by
// load to restore multi-word score layouts (LruLfu) after the keys are placed.
template <typename Table>
__global__ void scatter_score_blocks_kernel(
    Table table, int64_t bkt_begin, int64_t n,
    const int64_t *__restrict__ slots, const ScoreType *__restrict__ vals) {
  using Bucket = typename Table::BucketType;
  int64_t i = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;
  if (i >= n)
    return;
  int64_t s = slots[i];
  if (s < 0)
    return;
  int64_t ns = table.num_scores();
  int64_t bc = table.bucket_capacity();
  Bucket b = table[bkt_begin + s / bc];
  int64_t it = s % bc;
  for (int64_t k = 0; k < ns; ++k)
    *b.scores(it, k) = vals[i * ns + k];
}

// Write (key, score words) at an EXACT table-relative slot -- the "precise
// write-back" primitive behind replay_increment. Unlike insert(), the slot is
// dictated by the caller (it comes from the source table's dump) instead of
// being probed for.
//
// A key is only ever findable inside its own home bucket, so a slot from a
// source table is usable only when this table's capacity/bucket_capacity match
// the source's. That is verified per key here; a key whose slot falls outside
// its home bucket is reported with status -1 and left untouched, and the caller
// raises -- placing it anywhere else would make it unreachable.
//
// status[i]   = the slot written, or -1 when this key was not placed.
// same_key[i] = true when the slot's previous occupant was this very key, i.e.
//               the value row already belongs to it (its optimizer state can be
//               kept); false means the row must be re-initialised.
template <typename Table, int ProbingGroupSize>
__global__ void scatter_keys_at_slots_kernel(
    Table table, int64_t const *__restrict__ table_bucket_offsets,
    int *__restrict__ bucket_sizes, int64_t batch,
    typename Table::KeyType const *__restrict__ input_keys,
    int64_t const *__restrict__ table_ids, int64_t const *__restrict__ slots,
    ScoreType const *__restrict__ scores, int64_t *__restrict__ status,
    bool *__restrict__ same_key) {

  using KeyType = typename Table::KeyType;
  using Bucket = typename Table::BucketType;
  using Iter = typename Bucket::Iterator;

  auto tid = static_cast<int64_t>(blockIdx.x) * blockDim.x + threadIdx.x;

  for (int64_t i = tid; i < batch; i += gridDim.x * blockDim.x) {
    status[i] = -1;
    same_key[i] = false;

    KeyType key = input_keys[i];
    int64_t slot = slots[i];
    if (!Bucket::is_valid(key) || slot < 0)
      continue;

    int64_t bc = table.bucket_capacity();
    int64_t t_id = table_ids[i];
    int64_t bkt_begin = table_bucket_offsets[t_id];
    int64_t bkt_end = table_bucket_offsets[t_id + 1];
    int64_t table_cap = (bkt_end - bkt_begin) * bc;
    if (table_cap <= 0 || slot >= table_cap)
      continue;

    // The slot must land in the key's home bucket, otherwise no lookup would
    // ever probe it. This is the per-key half of the layout-compatibility check.
    int64_t hashcode = Table::hash(key);
    int64_t home_local = (hashcode % table_cap) / bc;
    if (home_local != slot / bc)
      continue;

    int64_t bucket_id = bkt_begin + home_local;
    Bucket bucket = table[bucket_id];
    int64_t ns = table.num_scores();
    Iter target = static_cast<Iter>(slot % bc);

    // The key is not searched for first: a replayed table is written only by
    // replay from one source, so a key is either absent or already at the slot
    // the source gives, never at some other slot in this bucket. Probing to rule
    // that out would cost a probe per key to find nothing.
    //
    // Take the target slot under the bucket's lock, as any writer must. One
    // attempt, no spin: a replay has the table to itself -- it cannot run
    // alongside a forward pass, whose prefetch state holds row indices this very
    // write invalidates -- and a delta's slots are distinct, one slot having
    // held one key in the source, so no other thread in this launch is aiming
    // here either. Finding the slot locked, or changed underfoot, therefore does
    // not mean "wait"; it means that assumption did not hold and this replay is
    // not safe. Leave status[i] at -1 and let the host raise.
    auto key_slot =
        reinterpret_cast<typename Bucket::AtomicKey *>(bucket.keys(target));
    KeyType old_key = key_slot->load(cuda::std::memory_order_relaxed);
    // Checked before the CAS, not folded into it: locking a slot that already
    // reads LockedKey would swap it for itself and report success.
    if (old_key == static_cast<KeyType>(Bucket::LockedKey))
      continue;
    KeyType expected_key = old_key;
    if (!bucket.try_lock(target, expected_key))
      continue;

    for (int64_t k = 0; k < ns; ++k)
      *bucket.scores(target, k) = scores[i * ns + k];
    *bucket.digests(target) = Bucket::key_to_digest(key);
    // Key last, with release: whoever reads this key afterwards sees the score
    // words and digest that belong to it.
    bucket.unlock(target, key);

    if (!Bucket::is_valid(old_key))
      atomicAdd(bucket_sizes + bucket_id, 1);

    status[i] = slot;
    same_key[i] = (old_key == key);
  }
}

} // namespace dyn_emb
